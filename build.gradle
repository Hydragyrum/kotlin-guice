buildscript {
    ext.guice_version = '4.1.0'
    ext.kotlin_version = '1.1.2-4'

    repositories {
        jcenter()
        mavenCentral()
        maven {
            url "https://plugins.gradle.org/m2/"
        }
    }

    dependencies {
        classpath "io.spring.gradle:dependency-management-plugin:1.0.0.RELEASE"
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
        classpath "org.jetbrains.dokka:dokka-gradle-plugin:0.9.14"
        classpath "org.junit.platform:junit-platform-gradle-plugin:1.0.0-M4"
        classpath "gradle.plugin.org.jlleitschuh.gradle:ktlint-gradle:2.0.1"
        classpath "me.champeau.gradle:jmh-gradle-plugin:0.3.1"
    }
}

subprojects {
    apply plugin: 'java'
    apply plugin: 'kotlin'
    apply plugin: 'io.spring.dependency-management'
    apply plugin: 'org.junit.platform.gradle.plugin'
    apply plugin: 'org.jetbrains.dokka'
    apply plugin: "org.jlleitschuh.gradle.ktlint"
    apply plugin: "me.champeau.gradle.jmh"

    sourceCompatibility = 1.8

    group 'com.authzee'
    version '1.0-SNAPSHOT'

    repositories {
        jcenter()
        mavenCentral()
    }

    dokka {
        outputFormat = 'html'
        outputDirectory = "$buildDir/javadoc"

        externalDocumentationLink {
            url =  new URL("http://google.github.io/guice/api-docs/4.1/javadoc/")
        }
    }

    junitPlatform {
        filters {
            engines {
                include 'spek'
            }
        }
    }

    jmh {
        // For some reason jmh, the annotation processor, or the jmh-gradle-plugin is creating
        // duplicates of the jmh generated classes; one in the build/jmh-generated-classes and
        // one in the build/classes directory. This leads to duplicates being found when copying
        // classes for the jar. Changing this to 'warn' allows us to run the benchmarks even though
        // something isn't configured correctly somewhere.
        duplicateClassesStrategy = 'warn'
    }

    sourceSets {
        jmh
    }

    dependencyManagement {
        dependencies {
            "org.jetbrains.kotlin:kotlin-stdlib-jre8:$kotlin_version"
        }
    }

    dependencies {
        jmh 'org.openjdk.jmh:jmh-core:1.19'
        jmh 'org.openjdk.jmh:jmh-generator-annprocess:1.19'
    }

    build.dependsOn ktlintCheck
}
